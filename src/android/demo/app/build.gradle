apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-parcelize'

//应用 FT plugin
apply plugin: 'ft-plugin'

android {
    def localProperty = getLocalProperties()

    buildFeatures {
        buildConfig = true
    }

    namespace 'com.cloudcare.ft.mobile.sdk.demo'
    compileSdk 34
    defaultConfig {
        applicationId "com.guance.mobile.sdk.demo"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 12
        versionName "1.2.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

//        buildConfigField "String", "CUSTOM_VALUE", "\"Custom value\""
//        buildConfigField "String", "DATAKIT_URL", "\"${ft_env.datakitRUMUrl}\""
//        buildConfigField "String", "DATAWAY_URL", "\"${ft_env.datawayRUMUrl}\""
//        buildConfigField "String", "DATAWAY_CLIENT_TOKEN", "\"${ft_env.cliToken}\""
//        buildConfigField "String", "RUM_APP_ID", "\"${ft_env.rumAppid}\""
//        buildConfigField "String", "DEMO_API_URL", "\"${ft_env.demoAPIURL}\""
//        buildConfigField "String", "ENV", "\"${ft_env.prodTestEnv}\""
    }
    signingConfigs {
        release {
            storeFile file(localProperty.get("keyStorePath"))
            storePassword localProperty.get("storePassword")
            keyAlias localProperty.get("keyAlias")
            keyPassword localProperty.get("keyPassword")
        }

    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.15"
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    flavorDimensions = ['tier']

    productFlavors {

        prodTest {
            buildConfigField "String", "CUSTOM_VALUE", "\"Custom value\""
            buildConfigField "String", "DATAKIT_URL", "\"${ft_env.datakitRUMUrl}\""
            buildConfigField "String", "DATAWAY_URL", "\"${ft_env.datawayRUMUrl}\""
            buildConfigField "String", "DATAWAY_CLIENT_TOKEN", "\"${ft_env.cliToken}\""
            buildConfigField "String", "RUM_APP_ID", "\"${ft_env.rumAppid}\""
            buildConfigField "String", "DEMO_API_URL", "\"${ft_env.demoAPIURL}\""
            buildConfigField "String", "ENV", "\"${ft_env.prodTestEnv}\""
            dimension "tier"
        }

        prodPublish {
            buildConfigField "String", "CUSTOM_VALUE", "\"Custom value\""
            buildConfigField "String", "DATAKIT_URL", "\"${ft_env.datakitRUMUrl}\""
            buildConfigField "String", "DATAWAY_URL", "\"${ft_env.datawayRUMUrl}\""
            buildConfigField "String", "DATAWAY_CLIENT_TOKEN", "\"${ft_env.cliToken}\""
            buildConfigField "String", "RUM_APP_ID", "\"${ft_env.rumAppid}\""
            buildConfigField "String", "DEMO_API_URL", "\"${ft_env.demoAPIURL}\""
            buildConfigField "String", "ENV", "\"${ft_env.prodPublishEnv}\""
            dimension "tier"
        }

    }

}

FTExt {
    showLog = true
    autoUploadMap = true
    autoUploadNativeDebugSymbol = true
    datakitUrl = ft_env.datakitRUMUrl
    datawayToken = ft_env.datawayToken
    appId = ft_env.rumAppid
    env = 'common'

    prodFlavors {
        //Favor 中的配置会覆盖主项目 FTExt 中的配置
        prodTest {
            autoUploadMap = true
            autoUploadNativeDebugSymbol = true
            datakitUrl = ft_env.datakitRUMUrl
            datawayToken = ft_env.datawayToken
            appId = ft_env.rumAppid
            env = ft_env.prodTestEnv

        }
        prodPublish {
            autoUploadMap = true
            autoUploadNativeDebugSymbol = true
            datakitUrl = ft_env.datakitRUMUrl
            datawayToken = ft_env.datawayToken
            appId = ft_env.rumAppid
            env = ft_env.prodPublishEnv
        }
    }
}

def getLocalProperties() {

    def storePassword = System.getenv("storePassword")
    def keyAlias = System.getenv("keyAlias")
    def keyPassword = System.getenv("keyPassword")
    def keystorePath = System.getenv("keystorePath")


    Properties properties = new Properties()
    if (storePassword != null && keyAlias != null && keyPassword != null && keystorePath != null) {
        println("getLocalProperties for env")
        properties.setProperty("storePassword", storePassword)
        properties.setProperty("keyAlias", keyAlias)
        properties.setProperty("keyPassword", keyPassword)
        properties.setProperty("keystorePath", keystorePath)

    } else {
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
    }
    return properties
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation project(path: ':nativelib')


    //================FT-SDK需要使用的引用=================//
    //FT-SDK引用
    implementation 'com.cloudcare.ft.mobile.sdk.tracker.agent:ft-native:1.1.1'

    implementation('com.cloudcare.ft.mobile.sdk.tracker.agent:ft-sdk:1.7.0-alpha15')
    implementation 'com.cloudcare.ft.mobile.sdk.tracker.agent:ft-session-replay:0.1.1-alpha01'
    implementation 'com.cloudcare.ft.mobile.sdk.tracker.agent:ft-session-replay-material:0.1.1-alpha01'

//    implementation files('libs/ft-session-replay-debug.aar')
//    implementation files('libs/ft-sdk-debug.aar')
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.picasso:picasso:2.8'

    //需用监控网络数据时，添加该依赖，实现网络引擎
//    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    implementation 'com.squareup.okhttp3:okhttp:4.5.0'

    implementation "androidx.navigation:navigation-compose:2.7.5"
    implementation "androidx.activity:activity-compose:1.6.0"
    implementation "androidx.compose.material3:material3:1.1.0"
    implementation "androidx.compose.ui:ui:1.6.0"
    implementation "androidx.compose.ui:ui-tooling-preview:1.2.0"

    //otel SDK
    implementation("io.opentelemetry:opentelemetry-sdk:1.36.0")
    implementation("io.opentelemetry:opentelemetry-exporter-otlp:1.36.0") // OTLP Exporter
    implementation("io.opentelemetry:opentelemetry-api:1.36.0")
    implementation("io.opentelemetry:opentelemetry-context:1.36.0")

}